cimport "stdlib.h";
cimport "stdio.h";
cimport "../../include/ast.h";
cimport "../../include/types.h";

type PurityContext = struct {
    ast_program *ast;
    compiler *comp;
    ast_toplevel *current_function;
    i32 errors;
};

impure fn purity_visitor(ast_visitor_node *node, void *user_data) -> VisitorResult;
impure fn check_purity_expr(PurityContext *ctx, ast_expr *ast) -> i32;

pub impure fn purity_new(ast_program *ast, compiler *comp) -> PurityContext* {
    let mut result = as PurityContext *calloc(1, as u64 size<PurityContext>);
    result->ast = ast;
    result->comp = comp;
    result
}

pub impure fn purity_run(PurityContext *ctx) -> i32 {
    ast_visit(ctx->comp, ctx->ast, purity_visitor, ctx);
    ctx->errors
}

pub impure fn purity_destroy(PurityContext *ctx) -> void {
    free(ctx);
}

impure fn purity_visitor(ast_visitor_node *node, void *user_data) -> VisitorResult {
    let mut ctx = as PurityContext* user_data;

    if node->toplevel != nil {
        let toplevel = node->toplevel;
        // AST_DECL_TYPE_FDECL
        if toplevel->type == 2 {
            ctx->current_function = toplevel;
        } else {
           ctx->current_function = nil;
        };
    } else if ctx->current_function != nil && node->expr != nil {
        let func = ctx->current_function;
        let tl = func->toplevel;
        let fd = tl.fdecl;
        let flags = fd.flags;
        // DECL_FLAG_IMPURE
        if (flags & (1 << 4)) == 0 {
            check_purity_expr(ctx, node->expr);
        };
    };

    if ctx->errors > 0 {
        VisitorResult::VisitorStop
    } else {
        VisitorResult::VisitorContinue
    }
}

impure fn check_purity_expr(mut PurityContext *ctx, ast_expr *ast) -> i32 {
    ctx->errors = ctx->errors + match ast->type {
        // AST_EXPR_TYPE_DEREF
        7 => {
            if ast->expr.deref.is_ptr {
                compiler_log(ctx->comp, LogLevel::LogLevelError, "purity", "dereference of pointers is not allowed in pure functions");
                1
            } else {
                0
            }
        }

        // AST_EXPR_TYPE_CALL
        8 => {
            if ast->expr.call.fdecl == nil {
                // fn ptrs are always impure
                compiler_log(ctx->comp, LogLevel::LogLevelError, "purity", "calling function pointers is not allowed from pure functions (attempted usage from %s)", ast->expr.call.ident.value.identv.ident);
                1
            } else if (ast->expr.call.fdecl->flags & (1 << 4)) != 0 {
                // DECL_FLAG_IMPURE
                compiler_log(ctx->comp, LogLevel::LogLevelError, "purity", "call from pure function to impure function %s is not allowed", ast->expr.call.ident.value.identv.ident);
                1
            } else {
                0
            }
        }

        // AST_EXPR_TYPE_LOAD
        15 => {
            compiler_log(ctx->comp, LogLevel::LogLevelError, "purity", "dereference of pointers is not allowed in pure functions");
            1
        }

        _ => {
            0
        }
    };

    0
}
