start: module

module: (foreign | fndecl | tydecl | cimport | import)+

foreign: "foreign" STRING "{" (fndecl)* "}"

fndecl: (fnattr)* "fn" IDENT "(" params? ")" ("->" type)? (fnintrinsic)? (block | ";")
tydecl: "type" IDENT "=" (type | ("struct" | "enum") "{" decl_field_list "}") ";"
cimport: "cimport" STRING ";"
import: "import" STRING ";"

fnattr: "impure" | "pub"
fnintrinsic: "intrinsic" STRING type ("," type)*

type: NUMERIC_TYPE | VEC_TYPE | MAT_TYPE | FLOAT_TYPE | VOID_TYPE | STR_TYPE | type_cell | IDENT

type_cell: "Cell" "<" type ">"

decl_field_list: struct_field_list | enum_field_list

struct_field_list: (struct_field_decl ";")+
struct_field_decl: type IDENT

enum_field_list: (enum_field_decl ",")* enum_field_decl (",")?
enum_field_decl: IDENT | IDENT "(" type ")"

params: param ("," param)* ("," "*")?
param: type IDENT

block: "{" (stmt ";")* expr? "}"

stmt: "let" IDENT (":" type)? "=" (initattr)* (expr | initializer) // variable declaration
    | "while" expr block // while loop
    | IDENT ":=" expr // modification of mutable
    | expr // expression with unused result

initattr: "cell"

expr: if_expr
    | or_expr

if_expr: "if" expr block ("else" block)?

initializer: mat_initializer // matrix
    | vec_initializer // vector
    | "{" (expr ("," expr)*)? "}"

mat_initializer: "<" vec_initializer ("," vec_initializer)* ">"

vec_initializer: "<" expr ("," expr)* ">"

?or_expr: and_expr ("||" and_expr)*
?and_expr: cmp_expr ("&&" cmp_expr)*
?cmp_expr: add_expr (("=="|"!="|"<"|"<="|">"|">=") add_expr)*
?add_expr: mul_expr (("+"|"-") mul_expr)*
?mul_expr: unary (("*"|"/"|"%") unary)*
?unary: ("!"|"-") unary | postfix
?postfix: primary ( "(" args? ")" | "." IDENT | "[" expr "]" )*
?primary: "&" IDENT | IDENT | constant | "(" expr ")" | "as" "<" type ">" "(" expr ")" | initializer | block
constant: INT | FLOAT | STRING
args: expr ("," expr)*

NUMERIC_TYPE: /(?:i|u)(?:8|16|32|64)/
VEC_TYPE:     /fvec[1-9][0-9]*/
MAT_TYPE:     /fmat[1-9][0-9]*x[1-9][0-9]*/
FLOAT_TYPE:   "float"
VOID_TYPE:    "void"
STR_TYPE:     "str"

IDENT: /[A-Za-z_][A-Za-z0-9_]*(?:-[A-Za-z0-9_]+)*/

INT: /0|[1-9][0-9]*/
FLOAT: /[0-9]+\.([0-9]+)?/
STRING: /"([^"\\]|\\.)*"/

%import common.WS
%ignore WS

COMMENT: "//" /[^\n]/*
%ignore COMMENT

ML_COMMENT: "/*" /.*\*\//
%ignore ML_COMMENT

